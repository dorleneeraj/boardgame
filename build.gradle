plugins {
    id 'java'
    id 'jacoco'
}


group 'com.board.games'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.logging.log4j:log4j-api:2.17.2'
    implementation 'org.apache.logging.log4j:log4j-core:2.17.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.3.1'
}

test {
    doLast {
        delete "$rootProject.projectDir/snakeladderlog/"
    }
    useJUnitPlatform()
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    
    manifest {
        attributes "Main-Class": "com.board.games.GameSimulator"
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}


jacocoTestReport {
    dependsOn test // tests are required to run before generating the report

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "com/board/games/domain/board/**",
                    "com/board/games/domain/token/**",
                    "com/board/games/domain/player/Player.class",
                    "com/board/games/domain/player/SLPlayersFactory.class",
                    "com/board/games/domain/game/GamesFactory.class",
                    "com/board/games/exception/**"
            ])
        }))
    }
}



test.finalizedBy jacocoTestReport
check.dependsOn jacocoTestCoverageVerification